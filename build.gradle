plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
}

architectury {
    minecraft = project.minecraft_version
    common(project.enabled_platforms.split(','))
}

loom {
    runConfigs.configureEach {
        ideConfigGenerated = true
    }
}

sourceSets.main.resources.srcDir 'src/main/generated'

allprojects {
    apply plugin: 'architectury-plugin'
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'maven-publish'

    group = 'cuteneko.catsplus'
    version = "${project.mod_version}+mc${project.minecraft_version}"

    architectury {
        minecraft = project.minecraft_version
    }

    loom {
        accessWidenerPath = file('src/main/resources/catsplus.accesswidener')
    }

    repositories {
        repositories {
            maven {
                name = 'Mod Menu'
                url = 'https://maven.terraformersmc.com/releases'
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

        modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
        modApi "dev.architectury:architectury:${project.architectury_version}"
    }

    configurations {
        common
        shadowCommon
        compileClasspath.extendsFrom common
        runtimeClasspath.extendsFrom common
        developmentForge.extendsFrom common
        developmentFabric.extendsFrom common
        developmentNeoForge.extendsFrom common
        developmentQuilt.extendsFrom common
    }

    def targetJavaVersion = 17
    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            it.options.release = targetJavaVersion
        }
    }

    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
        withSourcesJar()
    }

    jar {
        from('LICENSE') {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }

    processResources {
        var resourcesToReplace = ['build.properties', 'META-INF/mods.toml', 'pack.mcmeta', 'fabric.mod.json']

        var replaceTokens = [
                minecraft_version: minecraft_version,
                mod_version: mod_version,
                forge_loader_version: forge_loader_version,
                forge_version: forge_version,
                fabric_loader_version: fabric_loader_version,
                fabric_api_version: fabric_api_version,
                neoforge_version: neoforge_version,
                architectury_version: architectury_version
        ]

        inputs.properties replaceTokens
        filteringCharset 'UTF-8'

        filesMatching(resourcesToReplace) {
            expand replaceTokens
        }
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                name = 'YuluoMaven'

                def ENV = System.getenv();

                def releaseUrl = 'https://maven.yuluo.dev/repository/maven-releases/'
                def snapshotUrl = 'https://maven.yuluo.dev/repository/maven-snapshots/'
                url = ENV.MOD_RELEASE ? releaseUrl : snapshotUrl

                credentials {
                    username ENV.MOD_MAVEN_USER
                    password ENV.MOD_MAVEN_PASS
                }
            }
        }
    }
}
